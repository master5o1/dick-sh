#!/usr/bin/env zsh
typeset -A _dick_features
declare -A _dick_memory
_dick_hand=0

_longdick_allowed_variable_names="johnson dick cock schlong penis dong"

## helper function to parse `8====D` number literals
## also extracts variable values from memory.
_dick_value() {
    local value=0
    if [[ "$1" =~ ^8=*D$ ]]; then
        value=$(($(echo "$1" | grep -o "=" | wc -l)))
        echo $value
        return
    fi
    echo $_dick_memory["$1"]
}

## helper function to parse extract HEREDOC and
## de-indent to ensure an inner `EOF` does not
## have preceeding spaces. 
_parse_heredoc_block() {
    local raw=$(cat)
    local ident=`echo $raw | head -n 1 | sed -n 's/^\([[:space:]]*\).*/\1/p'`
    echo $raw | sed "s/^$ident//"
}

# DICK <name> <value>
# 
# Assign a <value> to a <name> in memory.
# @param $1 {string} name of variable.
# @param $2 {string | numeric} value to assign. May be a variable name or Dick numeric literal.
DICK() {
    local value=$(_dick_value $2)
    _dick_memory["$1"]=$value
}

# GRIP <prompt>
# 
# Prompts user to enter a Dick numeric literal.
# Value is assigned to the Hand.
# @param $1 {string} text to display to the user.
GRIP() {
    local prompt="$1"
    local input=""
    printf "$prompt ðŸ«³ "
    read input
    while [[ ! "$input" =~ ^8=*D$ ]]; do
        printf "ðŸ˜¡ $prompt ðŸ«³ "
        read input
    done
    _dick_hand=$(_dick_value $input)
}

# GRAB <value>
# 
# Assigns <value> to the Hand.
# @param $1 {string | numeric} value to assign. May be a variable name or Dick numeric literal.
GRAB() {
    _dick_hand=$(_dick_value $1)
}

# RELEASE <name> <value>
# 
# Assign a value of Hand to a <name> in memory.
# @param $1 {string} name of variable.
RELEASE() {
    _dick_memory["$1"]=$_dick_hand
}

# LONDICK <value>
# 
# Add <value> to value of the Hand.
# @param $1 {string | numeric} value to add. May be a variable name or Dick numeric literal.
LONGDICK() {
    local operand=$(_dick_value $1)
    _dick_hand=$((_dick_hand + $operand))
}

# SMALLDICK <value>
# 
# Subtract <value> from value of the Hand.
# @param $1 {string | numeric} value to subtract. May be a variable name or Dick numeric literal.
SMALLDICK() {
    local operand=$(_dick_value $1)
    _dick_hand=$((_dick_hand - $operand))
}

# HUGEDICK <value>
# 
# Multiply value of the Hand by <value>.
# @param $1 {string | numeric} value to multiply. May be a variable name or Dick numeric literal.
HUGEDICK() {
    local operand=$(_dick_value $1)
    _dick_hand=$((_dick_hand * $operand))
}

# TINYDICK <value>
# 
# Divide value of the Hand by <value>.
# @param $1 {string | numeric} value to divide. May be a variable name or Dick numeric literal.
TINYDICK() {
    local operand=$(_dick_value $1)
    _dick_hand=$((_dick_hand / $operand))
}

# DRIBBLEDICK <value>
# 
# Modulo value of the Hand by <value>.
# @param $1 {string | numeric} value to modulo. May be a variable name or Dick numeric literal.
DRIBBLEDICK() {
    local operand=$(_dick_value $1)
    _dick_hand=$((_dick_hand % $operand))
}

# PEE [LINE]
# 
# Output numeric value of Hand to STDIN.
# @param $1 {"LINE"} (optional) Append "\r\n" to print a new line.
PEE() {
    local lineending=""
    if [[ $1 == "LINE" ]]; then
        lineending="\r\n"
    fi
    printf "${_dick_hand}${lineending}"
}

# WEE [LINE]
# 
# Output ASCII character value of Hand to STDIN.
# @param $1 {"LINE"} (optional) Append "\r\n" to print a new line.
WEE() {
    local lineending=""
    if [[ $1 == "LINE" ]]; then
        lineending="\r\n"
    fi
    printf "\x$(printf %x "$_dick_hand")${lineending}"
}


# STROKE <<HEREDOC
# 
# A while loop that evaluates the `<<HEREDOC` string on each iteration of the loop.
# Loop condition is while value of Hand is greater than or equal to zero.
# 
# @example print numbers from 5 to 0.
# GRAB 8=====D
# STROKE <<CUM
#   PEE LINE
#   SMALLDICK 8=D
# CUM
STROKE() {
    local block=$(_parse_heredoc_block)
    while [[ $_dick_hand -ge 0 ]]; do
        eval "$block"
    done
}

# EDGING <<HEREDOC
# 
# Evaluates the `<<HEREDOC` string IF value of Hand is equal to zero.
# 
# @example print value if that number is divisible by five.
# GRAB value
# DRIBBLEDICK 8=====D
# EDGING <<CUM
#   GRAB value
#   PEE LINE
# CUM
EDGING() {
    local block=$(_parse_heredoc_block)
    if [[ "$_dick_hand" -eq 0 ]]; then
        eval "$block"
    fi
}

# BAG <name> [arg1, arg2, ...] <<HEREDOC
# 
# Defines a function or procedure.
# @param $1 {string} name of the function.
# @param $2...$n {string} parameters of the function.
#
# @example
# BAG P_DIV_5 arg <<END
#   RELEASE save_hand
#   GRAB arg
#   DRIBBLEDICK 8=====D
#   EDGING <<CUM
#     GRAB arg
#     PEE LINE
#   CUM
#   GRAB save_hand
# END
#
# P_DIV_5 8=====D
BAG() {
    local name="$1"
    local block=$(_parse_heredoc_block)

    shift
    for ((i=1; i <= $#; i++)); do
        block="${block//${@[$i]}/\$$i}"
    done

    eval "$name(){;$block;}"
}


# LOOK! condition
# 
# A LongDick conditional that evaluates the inner block
# of indented code if the condition resolves true.
#
# @example print value of johnson if it is odd.
# GRAB 8===D
# RELEASE johnson
# DRIBBLEDICK 8==D
# RELEASE dong
# LOOK! dong IS JUST LIKE 8=D!
#     GRAB johnson
#     PEE LINE
# DICK JOKES ARE IMMATURE, SERIOUSLY
__long_dick_conditional() {
    local operand1=$(_dick_value $1)
    local operator=$3
    local operand2=$(_dick_value $5)
    local block=$(_parse_heredoc_block)

    case $operator in
        "BIGGER")
            if [[ $operand1 -gt $operand2 ]]; then
                eval "$block"
            fi
            ;;
        "SMALLER")
            if [[ $operand1 -lt $operand2 ]]; then
                eval "$block"
            fi
            ;;
        "JUST")
            if [[ $operand1 -eq $operand2 ]]; then
                eval "$block"
            fi
            ;;
    esac
}

# COCK! loop
# 
# A LongDick conditional that evaluates the inner block
# of indented code if the condition resolves true.
#
# @example print numbers ascending from 1 to 5.
# DICK johnson 8D
# DICK schlong 8=====D
# COCK GO FAST IF johnson IS SMALLER THAN schlong!
#   GRAB johnson
#   LONGDICK 8=D
#   PEE LINE
# ALRIGHT, STOP COCKING AROUND
__long_dick_loop() {
    local operand1=$(_dick_value $1)
    local operand2=$(_dick_value $5)
    local operator=$3
    local block=$(_parse_heredoc_block)

    case $operator in
        "BIGGER")
            while [[ $operand1 -gt $operand2 ]]; do
                eval "$block"
                operand1=$(_dick_value $1)
                operand2=$(_dick_value $5)
            done
            ;;
        "SMALLER")
            while [[ $operand1 -lt $operand2 ]]; do
                eval "$block"
                operand1=$(_dick_value $1)
                operand2=$(_dick_value $5)
            done
            ;;
        "JUST")
            while [[ $operand1 -eq $operand2 ]]; do
                eval "$block"
                operand1=$(_dick_value $1)
                operand2=$(_dick_value $5)
            done
            ;;
    esac
}


# preprocessor to rewrite LongDick conditional and loops
# to a shell-compatible version function call.
# eg,
# LOOK! dong IS JUST LIKE 8=D!
#   GRAB johnson
#   PEE LINE
# DICK JOKES ARE IMMATURE, SERIOUSLY
#
# becomes
#
# __long_dick_conditional dong IS JUST LIKE 8=D <<CUM
#   GRAB johnson
#   PEE LINE
# CUM
__prepocess_long_dick() {
    cat $1 \
      | sed -E 's/LOOK\! (.+)\!/__long_dick_conditional \1 <<CUM/g' \
      | sed 's/DICK JOKES ARE IMMATURE, SERIOUSLY/CUM/g' \
      | sed -E 's/COCK GO FAST IF (.+)\!/__long_dick_loop \1 <<CUM/g' \
      | sed 's/ALRIGHT, STOP COCKING AROUND/CUM/g'
}

__longdick_validate_variable_names() {
    if [[ "$_dick_features[strict-mode]" != "true" ]]; then
        return 0
    fi

    local _CLR_RED="\033[31m"
    local _CLR_RST="\033[0m"
    local file_name="$1"
    local exit_code=0
    local line_no=0
    local last_line=""
    local last_location=""
    while IFS= read -r line; do
        ((++line_no))
        if [[ "$line" =~ ^(DICK|RELEASE)\ ([^ ]+)(\ +.+)?$ ]]; then
            local opcode="${match[1]}"
            local name="${match[2]}"
            local spaces="${opcode//?/ } "
            local hint="${name//?/^} "
            local col_no=$(($(echo "$spaces" | grep -o " " | wc -l) + 1))
            local location="$(printf %02d:%02d "$line_no" "$col_no")"
            if [[ ! "$_longdick_allowed_variable_names" =~ "$name" ]]; then
                echo -e "${_CLR_RED}[ERROR] Invalid variable name '$name' at $file_name:$location${_CLR_RST}" >&2
                echo "${_CLR_RED}    ...${_CLR_RST} $last_line" >&2
                echo "${_CLR_RED}    ...${_CLR_RST} $line" >&2
                echo "        $spaces${_CLR_RED}$hint${_CLR_RST}" >&2
                echo "        variable '$name' is not one of '$_longdick_allowed_variable_names'" >&2
                echo >&2
                exit_code=1
            fi
        fi
        last_line="$line"
        last_location="$location"
    done < "$file_name"

    [[ $exit_code -ne 0 ]] && exit $exit_code
}

typeset -A _dick_default_features=(
  shebang-args      true
  strict-mode       false
  help              false
  exec              true
)
typeset -A _dick_help_text=(
  shebang-args      "reading arguments from shebang"
  strict-mode       "strict mode; limit to permitted variable names in LongDick."
  help              "show this help"
  exec              "execute after pre-processing."
)
typeset -A _dick_args_alias=(
  -s                    strict-mode
  --strict              strict-mode
  --strict-mode         strict-mode
  -h                    help
  --help                help
)
typeset -A _dick_alias_only=(
    help                true
)

_dick_features=("${(@kv)_dick_default_features}")

_dick_source_filename=""
_dick_show_help=false

__dick_parse_args() {
    while [[ "$#" -gt 0 ]]; do
        arg="$1"
        case $arg in
        --use-*) _dick_features[${1/--use-/}]=true; shift;;
        --no-*) _dick_features[${1/--no-/}]=false; shift;;
        -h|--help) _dick_features[help]=true; shift ;;
        *)
            if [[ -f "$1" ]]; then
                _dick_source_filename="$1"
            elif [[ "$_dick_default_features[$_dick_args_alias[$1]]" == "false" ]]; then
                _dick_features[$_dick_args_alias[$1]]=true
            fi
            shift
            ;;
        esac
    done
}

__dick_parse_args $@

if [[ "$_dick_features[help]" != "true" ]] && [[ -f $_dick_source_filename ]]; then
    if [[ "$_dick_features[shebang-args]" == "true" ]]; then
        __dick_parse_args $(head -n 1 "$_dick_source_filename")
    fi
    __longdick_validate_variable_names "$_dick_source_filename"

    if [[ "$_dick_features[exec]" == "true" ]]; then
    source <(__prepocess_long_dick "$_dick_source_filename")
    else
        __prepocess_long_dick "$_dick_source_filename"
    fi
else
    echo "Usage: dick [options] program.dick"
    echo
    echo "Options:"
    for key in "${(@k)_dick_help_text}"; do
        local is_default=""
        local line_desc=""
        local desc="$_dick_help_text[$key]"
        local indent=""
        if [[ "${_dick_default_features[$key]}" == "true" ]]; then
            printf "  --no-%-21s %s\n" "$key" "disable $desc"
        else
            if [[ "${_dick_alias_only[$key]}" != "true" ]]; then
                printf "  --use-%-20s %s\n" "$key" "enable $desc"
                desc=""
                indent="  "
            fi
            for flag_alias in "${(@k)_dick_args_alias}"; do
                if [[ "${_dick_args_alias[$flag_alias]}" == "$key" ]]; then
                    if [[ ! -z $indent ]]; then
                        printf "%s  %3s%-21s %s\n" "$indent" "${flag_alias}" "" "$desc"
                    else
                        printf "  %-26s %s\n" "${flag_alias}" "$desc"
                        indent="  "
                    fi
                    desc=""
                fi
            done
            echo
        fi
    done
fi